#############################################################  
# STOCHASTIC SEARCH VARIABLE SELECTION: probit REGRESSION ###
#############################################################

model {   
	
#likelihood 
for (i in 1:N){
	mu[i] <- beta0 + inprod(X[i,], beta)
   	z[i]  <- phi(mu[i]) 
	Y[i] ~ dbern(z[i]) # usual probit regression model 
} 

# tracing the visited model
for (j in 1:p){
	TempIndicator[j] <- g[j] * pow(2, j) 
}
mdl<- 1 + sum(TempIndicator[]) # model index in binary coding 


c1 <- 1 / (pow(tau_ss, 2)) #reciprocal of the spike variance (precision)
c2 <- c1 / (pow(c_ss, 2)) # reciprocal of the slab variance 
##remember that the Gaussian distribution
## is parametrized in terms of precision parameter

beta0 ~ dnorm(0, 0.01)

for(j in 1:p){
	bprior[j]<-0
	tprior[j] <-equals(g[j],0) * c1 + equals(g[j],1) * c2 
	    # this is c1 if the first condition is met, 
	    #  c2 if the second is met
	beta[j] ~ dnorm(bprior[j],tprior[j]) 
	g[j] ~ dbern(0.5) # g[j] ~ dbern(theta[j]) # this prior CANNOT be assumed with Stan
                          # theta[j] ~ dunif(0,1)
}

}
